#!/usr/bin/env gawk
#
# vim: set ts=4 sw=4 tw=0 et :
#
# Copyright (C) 2014-2017, University of Washington. All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice, this
# list of conditions and the following disclaimer.
#
# 2. Redistributions in binary form must reproduce the above copyright notice,
# this list of conditions and the following disclaimer in the documentation
# and/or other materials provided with the distribution.
#
# 3. Neither the name of the copyright holder nor the names of its contributors
# may be used to endorse or promote products derived from this software without
# specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
# ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
# FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
# DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
# SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
# CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
# OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
# OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

# This script is designed to process the log output from terraform
# when it applies a plan for creating Digitalocean droplets. It
# extracts information necessary for subsequent Ansible playbooks by
# generating the top level "hosts:" block for a YAML inventory file.

BEGIN {
    SUBSEP="@";
    FS=" ";
    domain="devops.do";
    debug=0;
}

# Toggles to determine whether public keys or fingerprints of
# keys are being exported from droplets when terraform creates
# droplets.
/BEGIN SSH HOST KEY FINGERPRINTS/ { fingerprint=1; }
/END SSH HOST KEY FINGERPRINTS/ { fingerprint=0; }
/BEGIN SSH HOST PUBLIC KEYS/ { publickey=1; }
/END SSH HOST PUBLIC KEYS/ { publickey=0; }

{
	# Extract IP address for droplet
	if (match($0, /digitalocean_droplet\.([^ ]*) \(remote-exec\): *Host: (.*)/, arr)) {
		ip_addr[arr[1]]=arr[2];
	}

	# Extract SSH public keys and fingerprints for droplet
	if (match($0, /digitalocean_droplet\.([^ ]*) \(remote-exec\): (ssh-[^ ]*) (.*$)/, arr)) {
		if (publickey) {
			if (debug) { printf "ssh_pubkey: %s %s %s\n", arr[1], arr[2], arr[3] > "/dev/stderr"; }
			ssh_pubkey[arr[1] SUBSEP arr[2]] = arr[2] FS arr[3]
		}
		if (fingerprint) {
			if (debug) { printf "ssh_fp: %s %s %s\n", arr[1], arr[2], arr[3] > "/dev/stderr"; }
			ssh_fp[arr[1] SUBSEP arr[2]] = arr[2] FS arr[3]
		}
	}
}

# Now output the information by constructing YAML variables
END {
    printf "---\n\n# THIS FILE IS GENERATED BY 'do.awk' SCRIPT. DO NOT EDIT.\n\n"
    printf "droplets:\n"
    printf "  hosts:\n", h
	for (h in ip_addr) {
		printf "    '%s.%s':\n", h, domain;
		printf "      ansible_host: '%s'\n", ip_addr[h];
		printf "      ssh_host_public_keys:\n";
		for (combined in ssh_pubkey) {
			if (debug) {
				printf "combined: %s\n", combined > "/dev/stderr";
				printf "ssh_pubkey[combined]: %s\n", ssh_pubkey[combined] > "/dev/stderr";
			}
			split(combined, arr, SUBSEP)
			if (debug) {
				printf "arr[1], arr[2]: %s, %s\n", arr[1], arr[2] > "/dev/stderr";
			}
			if (arr[1] == h) {
				printf "          - '%s.%s,%s %s'\n", h, domain, ip_addr[h], ssh_pubkey[combined];
			}
		}
		printf "      ssh_host_public_key_fingerprints:\n";
		for (combined in ssh_fp) {
			if (debug) {
				printf "combined: %s\n", combined > "/dev/stderr";
			}
			split(combined, arr, SUBSEP)
			if (debug) {
				printf "ssh_fp[combined]: %s\n", ssh_fp[combined] > "/dev/stderr";
			}
			if (arr[1] == h) {
				printf "        - '%s'\n", ssh_fp[combined];
			}
		}
	}
    printf "\n\# vim\: ft=ansible \:\n"
}
